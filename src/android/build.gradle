plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

def appVersionName = System.getenv('VERSION_NAME') ?: "1.0.0"
def appVersionCode = System.getenv('BUILD_NUMBER')?.toInteger() ?: 1

android {
    namespace "com.synervoz.switchboardexampledsp"
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionName appVersionName
        versionCode appVersionCode

        ndk {
            abiFilters = []
            abiFilters.addAll(rootProject.ext.target_architectures)
        }

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_TOOLCHAIN=clang', '-DANDROID_STL=c++_shared'
                cppFlags '-Wall'
                cFlags "-DSTDC_HEADERS"
                targets "SwitchboardExampleDSP"
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            java.srcDirs += "src/main/java"
            java.srcDirs += "../jvm/src/main/java"
            jni.srcDirs += "src/main/cpp"
            jni.srcDirs += "../../src/all/"
        }
    }

    buildFeatures {
        prefab true
        prefabPublishing true
    }
    
    prefab {
        SwitchboardExampleDSP {
            headers "src/main/cpp/include"
        }
    }

    packagingOptions {
        exclude("**/libc++_shared.so")
        exclude("**/libSwitchboardSDK.so")
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            version '3.22.1'
        }
    }

    ndkVersion rootProject.ext.ndk_version
}

dependencies {
    implementation name: 'SwitchboardSDK', ext: 'aar'
}